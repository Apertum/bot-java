plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id "maven-publish"
    id "java"
}

sourceCompatibility = versions.javaVersion

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

repositories {
    mavenCentral()
}

dependencies {
    compile "com.google.code.findbugs:jsr305:${versions.jsr305}"
    compile "com.squareup.okhttp3:okhttp:${versions.okhttp}"
    compile "com.google.code.gson:gson:${versions.gson}"
    compile "org.slf4j:slf4j-api:${versions.slf4j}"

    testCompile "junit:junit:${versions.junit}"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }
    developers {
        developer {
            id properties['developer.id']
            name properties['developer.name']
            email properties['developer.email']
        }
    }

    scm {
        url "https://github.com/mail-ru-im/bot-java"
    }
}

publishing {
    publications {
        ApiPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'ru.mail.im'
            artifactId 'bot-api'
            version '1.0'
            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'bot-api')
                root.appendNode('url', 'https://github.com/mail-ru-im/bot-java')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = properties['bintray.user']
    key = properties['bintray.key']
    publications = ['ApiPublication']
    pkg {
        repo = 'bot-java'
        name = 'im-bot-java'
        userOrg = 'Mail.Ru'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/mail-ru-im/bot-java.git'
        publicDownloadNumbers = false
        version {
            name = '1.0'
        }
    }
}